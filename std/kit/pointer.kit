struct RefCount {
    var count: Size = 1;
    var allocator: Box[Allocator];
}

/**
 * A smart pointer to type T that employs reference counting. Call `ref()` to
 * obtain an additional reference, and `release()` when the reference is
 * released; the memory will be reclaimed when there are no more active
 * references.
 */
abstract Shared[T]: Ptr[T] {
    public static function new(allocator: Box[Allocator]): Shared[T] {
        var ptr: Ptr[RefCount] = allocator.alloc(sizeof RefCount + sizeof T);
        ptr.count = 1;
        ptr.allocator = allocator;
        return (ptr + 1) as Shared[T];
    }

    /**
     * Increment the reference count and return the pointer.
     */
    public function ref(): Shared[T] {
        var meta = this.metadata();
        ++meta.count;
        return this;
    }

    /**
     * Decrement the reference count. If this causes the count to drop to 0,
     * free it.
     */
    public function release(): Bool {
        var meta = this.metadata();
        if meta.count == 0 {
            return true;
        } else if --meta.count == 0 {
            meta.allocator.free(meta);
            return true;
        } else {
            return false;
        }
    }

    public function active(): Bool {
        var meta = this.metadata();
        return meta.count > 0;
    }

    public function rc(): Size {
        var meta = this.metadata();
        return meta.count;
    }

    function metadata(): Ptr[RefCount] {
        return (this as Ptr[Void] as Ptr[RefCount]) - 1;
    }
}
